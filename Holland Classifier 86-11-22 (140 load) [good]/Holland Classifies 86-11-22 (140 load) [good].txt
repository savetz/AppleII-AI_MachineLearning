-------------------------------------------- SCREEN 000
( HOLLAND CLASSIFIER     <11/22/86>140) 
                                        
\ With these routines we can build      
\ a Holland Classifier. See             
\ Frey, Peter W., A Bit-Mapped          
\ Classifier, BYTE, November 1986,      
\ pp 161 - 172 and Schrodt, Philip A.,  
\ Predicting International Events,      
\ BYTE, November 1986, pp 177 - 192.    
                                        
: HOLLAND ;                             
                                        
150 151 THRU \ Bit operations           
152 154 THRU \ Compare strings          
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               (  4:17) 
-------------------------------------------- SCREEN 010
( BIT OPERATIONS         <11/22/86>150) 
                                        
\ Array of bit masks                    
                                        
CREATE MASKS 1 C,  2 C,  4 C,    8 C,   
             16 C, 32 C, 64 C, 128 C,   
                                        
: MASK  ( n -- n1 )                     
   \ Fetch mask for bit n               
   MASKS + C@ ;                         
                                        
: BYTE  ( adr n -- adr1 n1 )            
   \ Get adr and bit number of nth      
   \ bit starting at adr                
   8 /MOD ROT + SWAP ;                  
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               (  4:17) 
-------------------------------------------- SCREEN 011
( BIT OPERATIONS         <11/22/86>151) 
                                        
: SET  ( adr n -- )                     
   \ Set bit n                          
   BYTE MASK OVER C@ OR SWAP C! ;       
                                        
: -SET ( adr n -- )                     
   \ Reset bit n                        
   BYTE MASK NOT OVER C@ AND SWAP C! ;  
                                        
: ?SET  ( adr n -- f )                  
   \ Is bit n set?                      
   BYTE MASK SWAP C@ AND 0> ;           
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               (  4:17) 
-------------------------------------------- SCREEN 012
( BIT COMPARISON         <11/22/86>152) 
                                        
: BINARY  ( -- )                        
   \ Set BASE for                       
   \ binary input and output            
   2 BASE ! ;                           
                                        
: -MASK  ( n -- n1 )                    
   \ Flip all bits                      
   255 XOR ;                            
                                        
: COMPARE  ( n1 n2 -- n3 )              
   \ Set bits that are different        
   \ between n1 and n2                  
   OVER -MASK OVER -MASK AND            
   >R AND R> OR -MASK ;                 
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               (  4:17) 
-------------------------------------------- SCREEN 013
( BIT COMPARISON         <11/22/86>153) 
                                        
: #BITS  ( b -- n )                     
   \ Count number of set bits in        
   \ byte                               
   0                                    
   8 0 DO                               
         OVER I MASK AND                
         IF                             
           1+                           
         THEN                           
       LOOP SWAP DROP ;                 
                                        
: DIFFERENCE ( n1 n2 -- n3 )            
   \ Determine number of bits that      
   \ are different between n1 and n2    
   COMPARE #BITS ;                      
                                        
                                        
                                        
                                        
                                        
                                        
                               (  4:17) 
-------------------------------------------- SCREEN 014
( BIT COMPARISON         <11/22/86>154) 
                                        
VARIABLE TEMP                           
                                        
: SIMILARITY  ( adr adr -- n )          
   0 TEMP !                             
   COUNT 0 DO                           
             OVER I + C@                
             OVER I + C@                
             DIFFERENCE                 
             TEMP +!                    
           LOOP                         
           DDROP                        
           TEMP @ ;                     
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               (  4:17) 
-------------------------------------------- SCREEN 030
(                        <12/13/86>170) 
                                        
0 CONSTANT FALSE                        
1 CONSTANT TRUE                         
                                        
\ PREDITOR                              
\ See Wimble, Michael, "Artificial      
\ Intelligence, An Evolutionary Idea,"  
\ BYTE, May, 1977, p 26; June, 1977,    
\ p 100                                 
                                        
VARIABLE SEED                           
                                        
: RESEED                                
   78 @ SEED ! ;                        
                                        
RESEED                                  
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 031
(                        <12/13/86>171) 
                                        
: +SEED  ( -- )                         
   BEGIN                                
     SEED @ NOT                         
   WHILE                                
     RESEED                             
   REPEAT ;                             
                                        
: RANDOM  ( n -- n )                    
   +SEED                                
   SEED @ 259 * SEED ! 1 SEED +!        
   +SEED                                
   SEED @ 0 ROT U/MOD DROP ;            
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 032
(                        <12/13/86>172) 
                                        
127 CONSTANT MAXSTATES                  
                                        
VARIABLE 1STATE                         
VARIABLE 2STATE                         
VARIABLE CURSTATE                       
VARIABLE #STATES                        
5 CONSTANT MUTATIONS                    
                                        
CREATE MODEL 256 ALLOT                  
                                        
: @CELL1  ( n -- n )                    
   2* MODEL + C@ ;                      
                                        
: !CELL1  ( n n --)                     
   2* MODEL + C! ;                      
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 033
(                        <12/13/86>173) 
                                        
: @CELL2  ( n -- n )                    
   2* 1+ MODEL + C@ ;                   
                                        
: !CELL2  ( n n -- )                    
   2* 1+ MODEL + C! ;                   
                                        
: @REPORT  ( b -- )                     
   128 AND ;                            
                                        
: REPORT  ( f -- )                      
   IF   ." True  "                      
   ELSE ." False "                      
   THEN ;                               
                                        
: NOW  ( b -- )                         
   127 AND ;                            
                                        
: !PREDICT  ( n -- n )                  
   2 RANDOM                             
   IF  128 + THEN ;                     
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 034
(                        <12/13/86>174) 
                                        
: DUMMY  ( -- )                         
   0 1STATE !  1 2STATE !               
   0 CURSTATE !                         
   3 #STATES !                          
   #STATES @ 0                          
   DO                                   
     I 1+ I !CELL1                      
     I 1+ I !CELL2                      
   LOOP                                 
   0 #STATES @ 1- !CELL1                
   0 #STATES @ 1- !CELL2 ;              
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 035
(                        <12/13/86>175) 
                                        
: REVEAL  ( -- )                        
  CR ." First state:   " 1STATE @ .     
  CR ." Second state:  " 2STATE @ .     
  CR ." Current state: " CURSTATE @ .   
  CR                                    
  #STATES @ 0                           
  DO                                    
    CR I @CELL1                         
         ." State: " I .                
         DUP @REPORT ." Predict: "      
         REPORT                         
         NOW ." Next: " . CR            
       I @CELL2                         
         ." State: " I .                
         DUP @REPORT ." Predict: "      
         REPORT                         
         NOW ." Next: " . CR            
  LOOP ;                                
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 036
(                        <12/13/86>176) 
                                        
: ADVANCE     ( input -- )              
   CURSTATE @ 2* + MODEL + C@           
   DUP @REPORT REPORT                   
   NOW CURSTATE ! ;                     
                                        
: T  ( -- )                             
    CURSTATE @                          
    CR ." State:      " .               
    2 RANDOM                            
    CR DUP ." Input:   "                
    IF   ." True"                       
    ELSE ." False"                      
    THEN CR                             
    ." Predict: "                       
    ADVANCE CR ;                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 037
(                        <12/13/86>177) 
                                        
CREATE STATEVECTOR 128 ALLOT            
                                        
: CLEAR-SV  ( -- )                      
    128 0 DO                            
            0 I STATEVECTOR + C!        
          LOOP ;                        
                                        
: MARK   ( -- )                         
    #STATES @ 0                         
    DO                                  
      I @CELL1 NOW I = NOT              
      IF  1 I @CELL1 NOW                
          STATEVECTOR + C!              
      THEN                              
      I @CELL2 NOW I = NOT              
      IF  1 I @CELL2 NOW                
          STATEVECTOR + C!              
      THEN                              
    LOOP ;                              
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 038
(                        <12/13/86>178) 
                                        
: QQ  ( -- )                            
    #STATES @ 0                         
    DO  I STATEVECTOR + C@ .            
    LOOP ;                              
                                        
: UNFLAGGED?  ( -- n )                  
   \ Returns last unflagged state;      
   \ else -1 if no unflagged states     
   -1                                   
   #STATES @ 0                          
   DO                                   
     I STATEVECTOR + C@ NOT             
     IF  DROP I                         
     THEN                               
   LOOP ;                               
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 039
(                        <12/13/86>179) 
                                        
: REPOINT  ( from to -- )               
    #STATES @ 0                         
    DO  DUP I = NOT                     
        IF                              
         DDUP I @CELL1 NOW ROT =        
              IF  I @CELL1 @REPORT +    
                  I !CELL1              
              ELSE DROP                 
              THEN                      
         DDUP I @CELL2 NOW ROT =        
              IF  I @CELL2 @REPORT +    
                  I !CELL2              
              ELSE DROP                 
              THEN                      
        THEN                            
    LOOP DDROP ;                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 040
(                        <12/13/86>180) 
                                        
: INRANGE ( -- )                        
    #STATES @ 0                         
    DO                                  
      I @CELL1 DUP                      
      NOW #STATES @ < NOT               
      IF @REPORT 1STATE @ + I !CELL1    
      ELSE DROP                         
      THEN                              
      I @CELL2 DUP                      
      NOW #STATES @ < NOT               
      IF @REPORT 1STATE @ + I !CELL2    
      ELSE DROP                         
      THEN                              
    LOOP ;                              
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 041
(                        <12/13/86>181) 
                                        
: REMOVE  ( -- )                        
   TRUE                                 
   BEGIN  #STATES @ 1 > AND             
   WHILE  CLEAR-SV MARK UNFLAGGED?      
    DUP 0< NOT                          
    IF #STATES @ 1- STATEVECTOR + C@    
      IF #STATES @ 1- 2* MODEL + @      
         OVER         2* MODEL + !      
         #STATES @ 1- SWAP REPOINT      
      ELSE DROP                         
      THEN                              
      -1 #STATES +! TRUE                
      #STATES @ 1STATE @ > NOT          
      IF #STATES @ RANDOM 1STATE !      
      THEN  2 RANDOM                    
      IF   1STATE @ @CELL1 NOW 2STATE ! 
      ELSE 1STATE @ @CELL2 NOW 2STATE ! 
      THEN                              
    ELSE  DROP FALSE                    
    THEN                                
   REPEAT INRANGE ;                     
                               ( LRP  ) 
-------------------------------------------- SCREEN 042
(                        <12/13/86>182) 
                                        
: ADDSTATE  ( -- f )                    
   MAXSTATES 1- #STATES @ >             
   IF TRUE                              
      MUTATIONS RANDOM 0                
      DO  #STATES @ RANDOM              
          2* MODEL +                    
          DUP C@ @REPORT #STATES @ +    
          SWAP C!                       
      LOOP                              
      1STATE  @ !PREDICT                
      #STATES @ !CELL1                  
      1STATE  @ !PREDICT                
      #STATES @ !CELL2                  
      1 #STATES +!                      
   ELSE  FALSE                          
   THEN                                 
   REMOVE                               
   CR ." ADDSTATE" ;                    
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 043
(                        <12/13/86>183) 
                                        
: ANOTHER  ( n1 n2 -- n3 )              
    \ Generate random number            
    \ less than n2 and not equal        
    \ to n1                             
    BEGIN                               
      DDUP RANDOM DUP ROT =             
    WHILE                               
      DROP                              
    REPEAT                              
    >R DDROP R> ;                       
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 044
(                        <12/13/86>184) 
                                        
: HIDE  ( n -- )                        
    \ Remove all pointers to state n    
    #STATES @ 0                         
    DO DUP I @CELL1 NOW =               
    IF  DUP #STATES @ ANOTHER           
        I @CELL1 @REPORT +              
        I !CELL1                        
    THEN                                
    DUP I @CELL2 NOW =                  
    IF  DUP #STATES @ ANOTHER           
        I @CELL2 @REPORT +              
        I !CELL2                        
    THEN                                
    LOOP DROP REMOVE ;                  
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 045
(                        <12/13/86>185) 
                                        
: DELSTATE  ( -- f )                    
   #STATES @ 1 >                        
   IF   TRUE                            
        #STATES @ RANDOM                
        HIDE                            
   ELSE FALSE                           
   THEN CR ." DELSTATE" ;               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 046
(                        <12/13/86>186) 
                                        
: RANSTATE  ( -- f )                    
   #STATES @ 1 >                        
   IF   TRUE                            
        #STATES @ RANDOM \ > to         
        #STATES @ RANDOM \ > from       
        DUP              \ > from       
        2 RANDOM                        
        IF @CELL1 @REPORT SWAP +        
           SWAP !CELL1                  
        ELSE @CELL2 @REPORT SWAP +      
           SWAP !CELL2                  
        THEN REMOVE                     
   ELSE FALSE                           
   THEN CR ." RANSTATE" ;               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 047
(                        <12/13/86>187) 
                                        
: INISTATE  ( -- f )                    
   #STATES @ 1 >                        
   IF   TRUE                            
      BEGIN                             
        #STATES @ RANDOM                
        DUP 1STATE @ =                  
      WHILE                             
        DROP                            
      REPEAT                            
      1STATE !                          
      1STATE @ @CELL1 NOW 2STATE !      
   ELSE FALSE                           
   THEN CR ." INISTATE" ;               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 048
(                        <12/13/86>188) 
                                        
: PSISTATE  ( -- f )                    
   #STATES @ 1 >                        
   IF   TRUE                            
        2STATE @ 1STATE !               
        1STATE @ @CELL1 NOW 2STATE !    
   ELSE FALSE                           
   THEN CR ." PSISTATE" ;               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 049
(                        <12/13/86>189) 
                                        
CREATE PROBS                            
         20 , 40 , 60 , 80 , 100 ,      
                                        
: WEIGHTED  ( -- n )                    
   100 RANDOM                           
   5 0 DO I 2* PROBS + @ OVER >         
          IF DROP I LEAVE               
          THEN                          
       LOOP ;                           
                                        
: CHOOSE ( n -- f )                     
   :                                    
   DOES> SWAP 2* + @ EXECUTE ;          
                                        
CHOOSE MUTCHOICE ADDSTATE DELSTATE      
       RANSTATE INISTATE PSISTATE ;     
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 050
(                        <12/13/86>190) 
                                        
: MUTATE  ( -- )                        
   BEGIN                                
     WEIGHTED                           
     MUTCHOICE                          
   UNTIL ;                              
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 051
(                        <12/13/86>191) 
                                        
10 CONSTANT TRIALS                      
VARIABLE PRESENT                        
                                        
CREATE HISTORY TRIALS ALLOT             
                                        
                                        
: START  ( -- )                         
   TRIALS 0 DO 0 I HISTORY + C! LOOP    
   0 PRESENT ! ;                        
                                        
:  GET  ( -- n )                        
    CR ." Enter you decision: "         
    BEGIN                               
      KEY DUP EMIT                      
      DUP                               
      ASCII F = OVER ASCII T = OR NOT   
    WHILE                               
      CR ." Enter T or F: "             
      DROP                              
    REPEAT ;                            
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 052
(                        <12/13/86>192) 
                                        
: STORE ( n -- )                        
     ASCII T =                          
     PRESENT @ HISTORY + C!             
     1 PRESENT +!  ;                    
                                        
: RUN  ( -- )                           
   START                                
   TRIALS 0 DO  GET STORE               
            LOOP ;                      
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 053
(                        <12/13/86>193) 
                                        
CREATE STATS MAXSTATES 4 * ALLOT        
                                        
: CLR-STATS  ( -- )                     
   MAXSTATES4 * 0 DO                    
                    0 I STATS + C!      
                  LOOP ;                
                                        
: STAT-ADR  ( cf tf s -- adr )          
   \ cf = correct/incorrect flag        
   \ tf = true/false flag               
   \ s  = state                         
   4 * STAT +                           
   SWAP 2* + + ;                        
                                        
: @STAT ( cf tf s -- n )                
   STAT-ADR C@ ;                        
                                        
: INC-STAT  ( cf tf s -- )              
   STAT-ADR 1 SWAP +! ;                 
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 055
(                        <12/13/86>195) 
                                        
: MYSELF                                
    LATEST PFA CFA , ; IMMEDIATE        
                                        
: TREE                                  
    CREATE , ,                          
    DOES> MYSELF ;                      
                                        
: TREE                                  
    0 TREE ;                            
                                        
: SOUND                                 
    FIND ;                              
                                        
: SEE                                   
    FIND NFA ID. ;                      
                                        
: HEAR                                  
    FIND 4 + @ EXECUTE ;                
                                        
\ SOUND BEEP TREE MAPLE                 
\ SOUND 2BEEP MAPLE TRUNK               
                               ( LRP  ) 
-------------------------------------------- SCREEN 059
(                        <12/13/86>199) 
                                        
    3 CONSTANT WHITE                    
    2 CONSTANT GREEN                    
    1 CONSTANT MAGENTA                  
    0 CONSTANT BLACK                    
                                        
  100 CONSTANT SCREEN                   
  300 CONSTANT SIDE                     
 -230 CONSTANT ACORNER                  
 -180 CONSTANT BCORNER                  
                                        
VARIABLE X                              
VARIABLE Y                              
VARIABLE A                              
VARIABLE B                              
VARIABLE GAP                            
                                        
: CPOINT  ( n x y -- )                  
   ROT 2  < IF WHITE HCOLOR ELSE        
                BLACK HCOLOR            
            THEN HPLOT ;                
                                        
                                        
-------------------------------------------- SCREEN 060
( MANDELBROT             <12/13/86>200) 
                                        
: MANDELB  ( -- )                       
     0                                  
   BEGIN                                
     DUP 99 <                           
     X @ DUP * Y @ DUP * +              
     A @ +                              
      2000 < AND                        
   WHILE                                
     X @ DUP * Y @ DUP * -              
     A @ +                              
     X @ Y @ * 2* B @ +                 
     Y !                                
     X !                                
     1+                                 
   REPEAT ;                             
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 061
( MANDELZOOM             <12/13/86>201) 
                                        
: MZ          ( -- )                    
   WHITE HCOLOR HGR                     
   SIDE SCREEN / GAP !                  
   ACORNER A !                          
   SCREEN 1 DO                          
              A @ GAP @ + A !           
              BCORNER B !               
              SCREEN 1 DO B @ GAP @ +   
              B !                       
                       0   X !          
                       0   Y !          
                       MANDELB          
                       I J CPOINT       
                      LOOP              
             LOOP BEEP BEEP BEEP ;      
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 079
(                        <12/13/86>219) 
                                        
: @W0   ( ADR -- N )                    
    @ ;                                 
: @W1   ( ADR -- N )                    
    2+ @ ;                              
: @W2   ( ADR -- N )                    
    4 + @ ;                             
: @W3   ( ADR -- N )                    
    6 + @ ;                             
                                        
: !W0   ( N ADR -- )                    
    ! ;                                 
: !W1   ( N ADR -- )                    
    2+ ! ;                              
: !W2   ( N ADR -- )                    
    4 + ! ;                             
: !W3   ( N ADR -- )                    
    6 + ! ;                             
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 080
(                        <12/13/86>220) 
                                        
: NEURON ( -- )                         
   CREATE                               
     0 ,  \ W0                          
     0 ,  \ W1                          
     0 ,  \ W2                          
     0 ,  \ W3                          
   DOES>  ( X1 X2 -- Y1 )               
     >R DDUP * R> SWAP OVER @W3 * >R    
     SWAP OVER @W2 * R> +         >R    
     SWAP OVER @W1 * R> +               
     SWAP @W0           + 121 / ;       
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 081
(                        <12/13/86>221) 
                                        
VARIABLE SEED                           
                                        
: RESEED                                
   78 @ SEED ! ;                        
                                        
RESEED                                  
                                        
: +SEED  ( -- )                         
   BEGIN                                
     SEED @ NOT                         
   WHILE                                
     RESEED                             
   REPEAT ;                             
                                        
: RANDOM  ( n -- n )                    
   +SEED                                
   SEED @ 259 * SEED ! 1 SEED +!        
   +SEED                                
   SEED @ 0 ROT U/MOD DROP ;            
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 082
(                        <12/13/86>222) 
                                        
4 CONSTANT STEP                         
                                        
: BUMP  ( N -- N )                      
   STEP 200 RANDOM 100 - 100 */ +       
   DUP  10 > IF 20 - ELSE               
   DUP -10 < IF 20 +                    
             THEN THEN ;                
                                        
CREATE W  0 , 0 , 0 , 0 ,               
                                        
CREATE W* 0 , 0 , 0 , 0 ,               
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 083
(                        <12/13/86>223) 
                                        
VARIABLE NEG                            
VARIABLE POS                            
VARIABLE ZER                            
0 NEG !                                 
0 POS !                                 
0 ZER !                                 
                                        
: Z 1000 0 DO 20 RANDOM 10 -            
              BUMP                      
              DUP .                     
              DUP 0 < IF 1 NEG +! ELSE  
              DUP 0 > IF 1 POS +! ELSE  
                         1 ZER +!       
                      THEN THEN DROP    
           LOOP ;                       
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 110
(                        <12/13/86>199) 
                                        
VARIABLE X                              
VARIABLE Y                              
VARIABLE A                              
VARIABLE B                              
                                        
VARIABLE GAP                            
180 CONSTANT SIDE                       
-50 CONSTANT ACORNER                    
-50 CONSTANT BCORNER                    
                                        
: CPOINT  ( n x y -- )                  
   ROT 3 <                              
   IF                                   
     HPLOT                              
   ELSE                                 
     DDROP                              
   THEN ;                               
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 111
( MANDELBROT             <12/13/86>200) 
                                        
: MANDELB  ( -- )                       
   100 0 DO                             
          X @ DUP * Y @ DUP * +         
            A @ +                       
          2000 <                        
          IF                            
            X @ DUP * Y @ DUP * -       
              A @ +                     
            X @ Y @ * 2* B @ + Y        
              ! X !                     
          ELSE                          
            I LEAVE                     
          THEN                          
         LOOP ;                         
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 112
( MANDELZOOM             <12/13/86>201) 
                                        
: MZ          ( -- )                    
   3 HCOLOR HGR                         
   SIDE 100 / GAP !                     
   ACORNER A !                          
   100 1 DO                             
           A @ GAP @ + A !              
           BCORNER B !                  
           100 1 DO B @ GAP @ + B !     
                  0  X !                
                  0  Y !                
                    MANDELB             
                    I J CPOINT          
                 LOOP                   
        X @ . Y @ . A @ . B @ .         
         LOOP BEEP ;                    
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 120
(                        <12/13/86>199) 
                                        
VARIABLE X                              
VARIABLE Y                              
VARIABLE A                              
VARIABLE B                              
                                        
VARIABLE GAP                            
100 CONSTANT SIDE                       
SIDE 2/ NEGATE CONSTANT ACORNER         
SIDE 2/ NEGATE CONSTANT BCORNER         
                                        
: CPOINT  ( n x y -- )                  
   ROT DUP 3  < IF 3 HCOLOR DROP ELSE   
       DUP 12 < IF 0 HCOLOR DROP ELSE   
                  2 HCOLOR DROP         
               THEN THEN                
               HPLOT ;                  
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
-------------------------------------------- SCREEN 121
( MANDELBROT             <12/13/86>200) 
                                        
: MANDELB  ( -- )                       
     0                                  
   BEGIN                                
     DUP 99 <                           
     X @ DUP * Y @ DUP * +              
     A @ +                              
     1000 < AND                         
   WHILE                                
     X @ DUP * Y @ DUP * -              
     A @ +                              
     X @ Y @ * 2* B @ +                 
     Y !                                
     X !                                
     1+                                 
   REPEAT ;                             
                                        
                                        
                                        
                                        
                                        
                                        
                                        
-------------------------------------------- SCREEN 122
( MANDELZOOM             <12/13/86>201) 
                                        
: MZ          ( -- )                    
   3 HCOLOR HGR                         
   SIDE 100 / GAP !                     
   ACORNER A !                          
   SIDE 1 DO                            
           A @ GAP @ + A !              
           BCORNER B !                  
           SIDE 1 DO B @ GAP @ + B !    
                  0   X !               
                  0   Y !               
                      MANDELB           
                      I J CPOINT        
                  LOOP                  
         LOOP BEEP BEEP BEEP ;          
                                        
                                        
                                        
                                        
                                        
                                        
                                        
                               ( LRP  ) 
